define

#
# Entities
#

gene sub entity,
	has geneId,
    has chromosome,
    has location,
    has description,
    has type,
    has symbol,
    has fullName,
	plays annotatedEntity,
	plays encoder,
	plays interactingGene,
	plays contained;

go sub entity,
	has goId,
	has name,
	has namespace,
	has definition,
	has comment,
	plays functionalAnnotation;

protein sub entity,
	has accession,
	has name,
	has fullName,
	has alternativeName,
	has sequence,
	has sequenceLength,
	has sequenceMass,
	has proteinGene,
	has function,
	has proteinPathway,
	has subunit,
	has tissue,
	has ptm,
	has similarity,
	plays annotatedEntity,
	plays contained,
	plays encoded;

pathway sub entity,
	has pathwayId,
	has name,
	has disease,
	has summation,
	plays annotatedEntity,
	plays container;

mirna sub entity,
	has accession,
	has name,
	has description,
	has comment,
	has sequence,
	plays precursor,
	plays upRegulator,
	plays downRegulator,
	plays contained;
	
mirnaMature sub mirna,
	has product,
	has location,
	plays mature,
	plays mutated,
	plays interactingMiRNA,
	plays mirnaWithSNP;

mirnaSNP sub entity,
	has snpId,
	has chr,
	has mirStart,
	has mirEnd,
	has lostNum,
	has gainNum,
	plays snp;
	
cancer sub entity,
	has name,
	plays upRegulated,
	plays downRegulated,
	plays byCancer;
	
interaction sub entity,
	has database,
	has experiments,
	has supportType,
	plays interacting;
	

#
# Relations
#

annotation sub relationship,
	has evidence,
	has qualifier,
	has category,
	relates functionalAnnotation,
	relates annotatedEntity;

containing sub relationship,
	relates container,
	relates contained;
	
precursorOf sub relationship,
	relates precursor,
	relates mature;

regulation sub relationship is-abstract,
	relates regulator,
	relates regulated;

upRegulation sub regulation,
	relates upRegulator,
	relates upRegulated;

downRegulation sub regulation,
	relates downRegulator,
	relates downRegulated;

snpMutation sub relationship,
	relates mutated,
	relates snp;

encoding sub relationship,
	relates encoder,
	relates encoded;

interactionMiRNA sub relationship,
	relates interactingMiRNA,
	relates interacting;

interactionGene sub relationship,
	relates interacting,
	relates interactingGene;
	
matureWithSNPsByCancer sub relationship,
	relates byCancer,
	relates mirnaWithSNP;
	
	
#
# Roles
#

functionalAnnotation sub role;
annotatedEntity sub role;
container sub role;
contained sub role;
precursor sub role;
mature sub role;
mutated sub role;
snp sub role;

regulator sub role;
regulated sub role;
upRegulator sub regulator;
upRegulated sub regulated;
downRegulator sub regulator;
downRegulated sub regulated;

encoder sub role;
encoded sub role;

interacting sub role;
interactingMiRNA sub role;
interactingGene sub role;

byCancer sub role;
mirnaWithSNP sub role;

#
# attributes
#

ID sub attribute datatype string is-abstract;

# common attributes
location sub attribute datatype string;
description sub attribute datatype string;
symbol sub attribute datatype string;
name sub attribute datatype string;
fullName sub attribute datatype string;
comment sub attribute datatype string;
accession sub attribute datatype string;

# gene
geneId sub ID;
chromosome sub attribute datatype string;
type sub attribute datatype string;

# go
goId sub ID;
namespace sub attribute datatype string;
definition sub attribute datatype string;

# protein
alternativeName sub name;
proteinGene sub attribute datatype string;
function sub attribute datatype string;
proteinPathway sub attribute datatype string;
subunit sub attribute datatype string;
tissue sub attribute datatype string;
ptm sub attribute datatype string;
similarity sub attribute datatype string;
sequenceLength sub attribute datatype long;
sequenceMass sub attribute datatype long;

# pathway
pathwayId sub ID;
disease sub attribute datatype string;
summation sub attribute datatype string;

# annotation
evidence sub attribute datatype string;
qualifier sub attribute datatype string;
category sub attribute datatype string;

# mirna
product sub attribute datatype string;
sequence sub attribute datatype string;

# mirnaSNP
snpId sub ID;
chr sub attribute datatype string;
mirStart sub attribute datatype long;
mirEnd sub attribute datatype long;
lostNum sub attribute datatype long;
gainNum sub attribute datatype long;

# interaction
database sub attribute datatype string;
experiments sub attribute datatype string;
supportType sub attribute datatype string;
