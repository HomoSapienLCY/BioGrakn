define

#
# Entities
#

gene sub entity,
	plays annotatedEntity,
	plays identified,
	plays encoder,
	plays interactingGene,
	plays contained,
	has geneId,
    has locusTag,
    has chromosome,
    has location,
    has description,
    has type,
    has symbol,
    has fullName,
    has otherDesignations,
    has nomenclatureStatus;

go sub entity,
	plays functionalAnnotation,
	has goId,
	has name,
	has namespace,
	has definition,
	has comment;

protein sub entity,
	plays annotatedEntity,
	plays contained,
	plays identified,
	plays encoded,
	has name,
	has fullName,
	has alternativeName,
	has sequence,
	has sequenceLength,
	has sequenceMass,
	has proteinGene,
	has function,
	has proteinPathway,
	has subunit,
	has tissue,
	has ptm,
	has similarity;

pathway sub entity,
	plays annotatedEntity,
	plays container,
	has pathwayId,
	has name,
	has disease,
	has summation;

mirna sub entity,
	plays precursor,
	plays upRegulator,
	plays downRegulator,
	plays contained,
	has accession,
	has name,
	has description,
	has comment,
	has sequence;
	
mirnaMature sub mirna,
	plays mature,
	plays mutated,
	plays interactingMiRNA,
	plays mirnaWithSNP,
	has product,
	has location;

mirnaSNP sub entity,
	plays snp,
	has snpId,
	has chr,
	has mirStart,
	has mirEnd,
	has lostNum,
	has gainNum;
	
cancer sub entity,
	plays upRegulated,
	plays downRegulated,
	plays byCancer,
	has name;
	
proteinAccession sub entity,
	plays identifier,
	has accession;

geneName sub entity,
	plays identifier,
	has name;

interaction sub entity,
	plays interacting,
	has database,
	has experiments,
	has supportType;
	

#
# Relations
#

annotation sub relationship,
	relates functionalAnnotation,
	relates annotatedEntity,
	has evidence,
	has qualifier,
	has category;

containing sub relationship,
	relates container,
	relates contained;
	
precursorOf sub relationship,
	relates precursor,
	relates mature;

regulation sub relationship is-abstract,
	relates regulator,
	relates regulated;

upRegulation sub regulation,
	relates upRegulator,
	relates upRegulated;

downRegulation sub regulation,
	relates downRegulator,
	relates downRegulated;

snpMutation sub relationship,
	relates mutated,
	relates snp;
	
entityReference sub relationship,
	relates identifier,
	relates identified;

encoding sub relationship,
	relates encoder,
	relates encoded;

interactionMiRNA sub relationship,
	relates interactingMiRNA,
	relates interacting;

interactionGene sub relationship,
	relates interacting,
	relates interactingGene;
	
matureWithSNPsByCancer sub relationship,
	relates byCancer,
	relates mirnaWithSNP;
	
	
#
# Roles
#

functionalAnnotation sub role;
annotatedEntity sub role;
container sub role;
contained sub role;
precursor sub role;
mature sub role;
mutated sub role;
snp sub role;

regulator sub role;
regulated sub role;
upRegulator sub regulator;
upRegulated sub regulated;
downRegulator sub regulator;
downRegulated sub regulated;

identifier sub role;
identified sub role;

encoder sub role;
encoded sub role;

interacting sub role;
interactingMiRNA sub role;
interactingGene sub role;

byCancer sub role;
mirnaWithSNP sub role;

#
# Inference-rules
#

genesInPathways sub rule
when {
    $gene isa gene;
    $protein isa $protein;
    (encoder: $gene, encoded: $protein) isa encoding;
    (container: $pathway, contained: $protein) isa containing;
}
then {
    (container: $pathway, contained: $gene) isa containing;
};

maturesWithSNPsByCancer sub rule
when {
    $cancer isa cancer;
    $mirna isa mirna;
    $mature isa mirnaMature;
    (upRegulator: $mirna, upRegulated: $cancer) isa upRegulation;
    (precursor: $mirna, mature: $mature) isa precursorOf;
    (mutated: $mature, snp: $snp) isa snpMutation;
}
then {
    (byCancer: $cancer, mirnaWithSNP: $mature) isa matureWithSNPsByCancer;
};

#
# attributes
#

ID sub attribute datatype string is-abstract;

# common attributes
location sub attribute, datatype string;
description sub attribute, datatype string;
symbol sub attribute, datatype string;
name sub ID;
fullName sub name;
comment sub attribute, datatype string;
accession sub attribute, datatype string;

# gene
geneId sub ID;
locusTag sub attribute, datatype string;
chromosome sub attribute, datatype string;
type sub attribute, datatype string;
otherDesignations sub attribute, datatype string;
nomenclatureStatus sub attribute, datatype string;

# go
goId sub ID;
namespace sub attribute, datatype string;
definition sub attribute, datatype string;

# protein
alternativeName sub name;
proteinGene sub attribute, datatype string;
function sub attribute, datatype string;
proteinPathway sub attribute, datatype string;
subunit sub attribute, datatype string;
tissue sub attribute, datatype string;
ptm sub attribute, datatype string;
similarity sub attribute, datatype string;
sequenceLength sub attribute, datatype long;
sequenceMass sub attribute, datatype long;

# pathway
pathwayId sub ID;
disease sub attribute, datatype string;
summation sub attribute, datatype string;

# annotation
evidence sub attribute, datatype string;
qualifier sub attribute, datatype string;
category sub attribute, datatype string;

# mirna
product sub attribute, datatype string;
sequence sub attribute, datatype string;

# mirnaSNP
snpId sub ID;
chr sub attribute, datatype string;
mirStart sub attribute, datatype long;
mirEnd sub attribute, datatype long;
lostNum sub attribute, datatype long;
gainNum sub attribute, datatype long;

# interaction
database sub attribute, datatype string;
experiments sub attribute, datatype string;
supportType sub attribute, datatype string;
