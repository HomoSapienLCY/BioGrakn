insert

#
# Entities
#

gene sub entity
	plays annotatedEntity
	plays identified
	plays encoder
	plays interactingGene
	plays contained
	has geneId
    has locusTag
    has chromosome
    has location
    has description
    has type
    has symbol
    has fullName
    has otherDesignations
    has nomenclatureStatus;

go sub entity
	plays functionalAnnotation
	has goId
	has name
	has namespace
	has definition
	has comment;

protein sub entity
	plays annotatedEntity
	plays contained
	plays identified
	plays encoded
	has name
	has fullName
	has alternativeName
	has sequence
	has sequenceLength
	has sequenceMass
	has proteinGene
	has function
	has proteinPathway
	has subunit
	has tissue
	has ptm
	has similarity;

pathway sub entity
	plays annotatedEntity
	plays container
	has pathwayId
	has name
	has disease
	has summation;

mirna sub entity
	plays precursor
	plays upRegulator
	plays downRegulator
	plays contained
	has accession
	has name
	has description
	has comment
	has sequence;
	
mirnaMature sub mirna
	plays mature
	plays mutated
	plays interactingMiRNA
	plays mirnaWithSNP
	has product
	has location;

mirnaSNP sub entity
	plays snp
	has snpId
	has chr
	has mirStart
	has mirEnd
	has lostNum
	has gainNum;
	
cancer sub entity
	plays upRegulated
	plays downRegulated
	plays byCancer
	has name;
	
proteinAccession sub entity
	plays identifier
	has accession;

geneName sub entity
	plays identifier
	has name;

interaction sub entity
	plays interacting
	has database
	has experiments
	has supportType;
	

#
# Relations
#

annotation sub relation
	relates functionalAnnotation
	relates annotatedEntity
	has evidence
	has qualifier
	has category;

containing sub relation
	relates container
	relates contained;
	
precursorOf sub relation
	relates precursor
	relates mature;

regulation sub relation is-abstract
	relates regulator
	relates regulated;

upRegulation sub regulation
	relates upRegulator
	relates upRegulated;

downRegulation sub regulation
	relates downRegulator
	relates downRegulated;

snpMutation sub relation
	relates mutated
	relates snp;
	
entityReference sub relation
	relates identifier
	relates identified;

encoding sub relation
	relates encoder
	relates encoded;

interactionMiRNA sub relation
	relates interactingMiRNA
	relates interacting;

interactionGene sub relation
	relates interacting
	relates interactingGene;
	
matureWithSNPsByCancer sub relation
	relates byCancer
	relates mirnaWithSNP;
	
	
#
# Roles
#

functionalAnnotation sub role;
annotatedEntity sub role;
container sub role;
contained sub role;
precursor sub role;
mature sub role;
mutated sub role;
snp sub role;

regulator sub role;
regulated sub role;
upRegulator sub regulator;
upRegulated sub regulated;
downRegulator sub regulator;
downRegulated sub regulated;

identifier sub role;
identified sub role;

encoder sub role;
encoded sub role;

interacting sub role;
interactingMiRNA sub role;
interactingGene sub role;

byCancer sub role;
mirnaWithSNP sub role;

#
# Inference-rules
#

$genesInPathways isa inference-rule,
when {
    $gene isa gene;
    $protein isa $protein;
    (encoder: $gene, encoded: $protein) isa encoding;
    (container: $pathway, contained: $protein) isa containing;
},
then {
    (container: $pathway, contained: $gene) isa containing;
};

$matureWithSNPsByCancer isa inference-rule,
when {
    $cancer isa cancer;
    $mirna isa mirna;
    $mature isa mirnaMature;
    (upRegulator: $mirna, upRegulated: $cancer) isa upRegulation;
    (precursor: $mirna, mature: $mature) isa precursorOf;
    (mutated: $mature, snp: $snp) isa snpMutation;
},
then {
    (byCancer: $cancer, mirnaWithSNP: $mature) isa matureWithSNPsByCancer;
};

#
# Resources
#

ID sub resource datatype string is-abstract;

# common resources
location sub resource datatype string;
description sub resource datatype string;
symbol sub resource datatype string;
name sub ID;
fullName sub name;
comment sub resource datatype string;
accession sub resource datatype string;

# gene
geneId sub ID;
locusTag sub resource datatype string;
chromosome sub resource datatype string;
type sub resource datatype string;
otherDesignations sub resource datatype string;
nomenclatureStatus sub resource datatype string;

# go
goId sub ID;
namespace sub resource datatype string;
definition sub resource datatype string;

# protein
alternativeName sub name;
proteinGene sub resource datatype string;
function sub resource datatype string;
proteinPathway sub resource datatype string;
subunit sub resource datatype string;
tissue sub resource datatype string;
ptm sub resource datatype string;
similarity sub resource datatype string;
sequenceLength sub resource datatype long;
sequenceMass sub resource datatype long;

# pathway
pathwayId sub ID;
disease sub resource datatype string;
summation sub resource datatype string;

# annotation
evidence sub resource datatype string;
qualifier sub resource datatype string;
category sub resource datatype string;

# mirna
product sub resource datatype string;
sequence sub resource datatype string;

# mirnaSNP
snpId sub ID;
chr sub resource datatype string;
mirStart sub resource datatype long;
mirEnd sub resource datatype long;
lostNum sub resource datatype long;
gainNum sub resource datatype long;

# interaction
database sub resource datatype string;
experiments sub resource datatype string;
supportType sub resource datatype string;